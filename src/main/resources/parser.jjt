options {
//    DEBUG_PARSER = true;
    MULTI = true;
    NODE_DEFAULT_VOID = true;
    OUTPUT_DIRECTORY = "../parser";
    JJTREE_OUTPUT_DIRECTORY = "../java/tree";
}

PARSER_BEGIN(Parser)


package tree;
import parser.Parser;

//package parser;
//import tree.*;

public class Parser {

}
PARSER_END(Parser)

SKIP : {
    " "
|   "\t"
|   "\n"
|   "\r"
|   "\f"
|   <SINGLE_LINE_COMMENT : "//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")? >
|   <MULTI_LINE_COMMENT : "/*" (~["*"] | "*" ~["/"])* "*/" >
}

/** Reserved words. */
TOKEN : {
    <BOOLEAN : "boolean">
|   <CLASS : "class">
|   <ELSE : "else">
|   <EXTENDS: "extends">
|   <FALSE: "false">
|   <IF: "if">
|   <INT: "int">
|   <LENGTH : "length">
|   <MAIN : "main">
|   <NEW: "new">
|   <PUBLIC : "public">
|   <RETURN: "return">
|   <STATIC: "static">
|   <STRING : "String">
|   <SOP : "System.out.println">
|   <THIS: "this">
|   <TRUE: "true">
|   <VOID: "void">
|   <WHILE: "while">
}

/** Literals. */
TOKEN : {
    <INTEGER_LITERAL : ["0" - "9"](["0" - "9"])*>
|   <STRING_LITERAL : "\"" (~["\"","\\","\n","\r"])* "\"">
}

/** Identifiers. */
TOKEN : {
    <ID : ["_","a" - "z","A" - "Z"](["_","a" - "z","A" - "Z","0" - "9"])*>
}

/** Separators. */
TOKEN : {
    <LPAR : "(">
|   <RPAR : ")">
|   <LBRACE : "{">
|   <RBRACE : "}">
|   <LBRACKET : "[">
|   <RBRACKET : "]">
|   <SEMICOLON : ";">
|   <COMMA : ",">
|   <DOT : ".">
}

/** Operators. */
TOKEN : {
    <ASSIGN : "=">
|   <EQUALS : "==">
|   <NOTEQ : "!=">
|   <GT : ">">
|   <GTEQ : ">=">
|   <LESS : "<">
|   <LESSEQ : "<=">
|   <NOT : "!">
|   <OR : "||">
|   <AND : "&&">
|   <PLUS : "+">
|   <MINUS : "-">
|   <MUL : "*">
|   <DIV : "/">
}


ASTProgram Program() #Program:
{}
{
    MainClass() (ClassDecl())*
    {return jjtThis;}
}

void Identifier() #Identifier:
{
    Token t;
}
{
    t=<ID>
    {
        jjtThis.setName(t.image);
    }

}

void MainClass() #MainClass:
{ }
{
    <CLASS> Identifier() <LBRACE>
        <PUBLIC> <STATIC> <VOID> <MAIN> <LPAR> <STRING> <LBRACKET> <RBRACKET>  Identifier() <RPAR> <LBRACE>
            (Statement())*
        <RBRACE>
    <RBRACE>
}

void ClassDecl() #ClassDecl:
{ }
{
    <CLASS> Identifier() ((<EXTENDS> Identifier() #Inheritance) )?  <LBRACE>
         ((VarDecl())+ #VarDecls)?
         ((MethodDecl())+ #MethodDecls)?
    <RBRACE>
}

void VarDecl() #VarDecl:
{}
{
    Type() Identifier() <SEMICOLON>
}

void MethodDecl() #MethodDecl:
{}
{
    <PUBLIC> Type() Identifier() <LPAR> (FormalList() #Args) <RPAR> <LBRACE>
        (LOOKAHEAD(3) VarDecl())* (Statement())* (<RETURN> Exp())#Retrun <SEMICOLON>
    <RBRACE>
}

void FormalList():
{}
{
    (((Type() Identifier()) #Arg)  (FormalRest())*)?
}

void FormalRest():
{}
{
    <COMMA> ((Type() Identifier()) #Arg)
}

void Type() #Type:
{ ASTIdentifier n;}
{
     (<INT> {jjtThis.setType("int");}
|   <BOOLEAN> {jjtThis.setType("boolean");}
|   Identifier()
        {
            n = (ASTIdentifier)jjtree.popNode();
            jjtThis.setType(n.getName());
        }
    )
    (<LBRACKET> <RBRACKET>{jjtThis.setVector(true);})?
}

void Statement():
{}
{
    (<LBRACE> (Statement())* <RBRACE>) #Block
|   (<IF> <LPAR> Exp() <RPAR> Statement() <ELSE> Statement()) #If
|   (<WHILE> <LPAR> Exp() <RPAR> Statement()) #While
|   (<SOP> <LPAR> Exp() <RPAR>  <SEMICOLON>) #Print
|   LOOKAHEAD(3) (ExpUnary() <ASSIGN>  Exp() <SEMICOLON>) #Assignment
|   VarDecl()
}

void Exp():
{}
{
    ExpOr()
}

void ExpOr():
{}
{
    ExpAnd() ( <OR>  ExpAnd() #OrNode(2))*
}

void ExpAnd():
{}
{
    ExpRel() ( <AND>  ExpRel() #AndNode(2))*
}

void ExpRel():
{
    Token t = null;
}
{
    ExpAdd() (((t=<GT> | t=<GTEQ> | t=<LESS> | t=<LESSEQ> | t=<EQUALS> | t=<NOTEQ> ) {jjtThis.setName(t.image);} ExpAdd()) #RelNode(2))*

}

void ExpAdd():
{
    Token t= null;
}
{
    ExpMul() (((t=<PLUS> | t=<MINUS> ) {jjtThis.setName(t.image);} ExpMul() )#AddNode(2))*
}

void ExpMul():
{
    Token t = null;
}
{
    ExpUnary() (((t=<MUL> | t=<DIV> ) {jjtThis.setName(t.image);} ExpUnary() )#MulNode(2))*

}

void ExpUnary() #UnaryNode:
{
    Token t = null;
}
{
    (t=<MINUS> | t=<NOT>)?
    ExpPostfix()
    {
        if(t != null) {
            jjtThis.setName(t.image);
        }
    }
}

void ExpPostfix():
{}
{
    ExpFactor() ((<LBRACKET> Exp() <RBRACKET>) #VecAccess
                | LOOKAHEAD(2) (<DOT> Identifier() ((<LPAR> ExpList() <RPAR>)#FunctionCall)?)
                | <DOT> <LENGTH> )*
}

void ExpFactor():
{
    Token t;
}
{
    (t=<INTEGER_LITERAL> {jjtThis.setValue(t.image);} #Integer )
|   <TRUE> #True
|   <FALSE> #False
|   Identifier()
|   <THIS> #This
|   <LPAR> Exp() <RPAR>
|   LOOKAHEAD(2) <NEW> <INT> <LBRACKET> Exp() <RBRACKET>
|   <NEW> Identifier() <LPAR> <RPAR>
}

void ExpList():
{}
{
    (Exp() (ExpRest())*)?
}

void ExpRest():
{}
{
    <COMMA> Exp()
}


